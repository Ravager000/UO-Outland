# Copy Runebook Script by Jaseowns
# Requirements: two runetome, one filled one blank - enough blank runes and regs to mark/recall
# By default the runelist is 26 long (full runetome)
# You can remove the list elements from runes_to_copy_list
# Additional Info: 
#   This script removes a rune from your "from" runetome, recalls to that location
#   and use the description of that rune.  Once it recalls, it will attempt to Mark
#   a blank rune and rename based on that description
#   Assuming all goes well, it puts both runes back
#   WARNING: if your runes are unsafe locations, this may fail.

removelist runes_to_copy_list
createlist runes_to_copy_list
# You can comment these out to prevent running all runes
pushlist runes_to_copy_list 1
pushlist runes_to_copy_list 2
pushlist runes_to_copy_list 3
pushlist runes_to_copy_list 4
pushlist runes_to_copy_list 5
pushlist runes_to_copy_list 6
pushlist runes_to_copy_list 7
pushlist runes_to_copy_list 8
pushlist runes_to_copy_list 9
pushlist runes_to_copy_list 10
pushlist runes_to_copy_list 11
pushlist runes_to_copy_list 12
pushlist runes_to_copy_list 13
pushlist runes_to_copy_list 14
pushlist runes_to_copy_list 15
pushlist runes_to_copy_list 16
# The entire runebook is 16
pushlist runes_to_copy_list 17
pushlist runes_to_copy_list 18
pushlist runes_to_copy_list 19
pushlist runes_to_copy_list 20
pushlist runes_to_copy_list 21
pushlist runes_to_copy_list 22
pushlist runes_to_copy_list 23
pushlist runes_to_copy_list 24
pushlist runes_to_copy_list 25
pushlist runes_to_copy_list 26
#The entire runetome is 26 runes 

if counttype "recall rune" backpack 2882 < 26
    overhead "Cannot continue" 34
    overhead "You need at least 26 unmarked recall runes in your backpack" 88
    stop
endif

if counttype "recall rune" backpack 0 > 0
    overhead "Cannot continue" 34
    overhead "Please remove any marked runes from your bag" 88
    stop
endif

overhead "Select a runebook to copy FROM"
setvar runebook_copy_from
wait 500
overhead "Select a runebook to copy TO"
setvar runebook_copy_to


overhead "Beginning Copy..." 88

if not timerexists runes_to_copy_skill_check
    createtimer runes_to_copy_skill_check
    settimer runes_to_copy_skill_check 11000
endif

foreach rune_to_copy in runes_to_copy_list
    clearsysmsg 
    if mana < 20
        while mana < 31
            overhead "Waiting for at least 30 mana..." 22
            if timer runes_to_copy_skill_check >= 11000
                useskill 'meditation'
                settimer runes_to_copy_skill_check 0
            endif
            wait 1000
        endwhile
    endif
    
    overhead "Pulling rune..." 88
    overhead rune_to_copy 144

    # Drop rune from tome runebook
    dclick runebook_copy_from
    waitforgump 167090027
    gumpresponse 200
	waitforgump 167090027
	gumpresponse 15
	gumpclose 167090027
	
	# Drop rune from runebook
    #dclick runebook_copy_from
    #waitforgump 1551740969
    #gumpresponse 3
	#waitforgump 1551740969
    #gumpclose 1551740969
	
    if findtype "recall rune" backpack 0 as currentRune
        
        getlabel currentRune runeDescription
        overhead runeDescription 88
        cast Recall
        wft 3500
        target currentRune
        wait 1000
        if followers > 0
            say 'All guard me'
        endif        
        cast Mark
        wft 3500
        if insysmsg "thus ruining thy spell"
            overhead "Might be getting attacked... trying again."
            cast Mark
            wft 3500
        endif
        targettype "recall rune" backpack 2882
        lift currentRune
        drop runebook_copy_from 0 0 0
        wait 500

        if findtype "recall rune" backpack 0 as newRune
            overhead "Rename and move to new book" 88
            dclick newRune
            wait 500
            promptresponse runeDescription
            wait 500
            lift newRune
            drop runebook_copy_to 0 0 0
        else
            overhead "Warning!" 34
            overhead "Unable to find copied rune" 88
        endif
    else
        overhead "Might be done..." 88
    endif
	 if followers > 0
            say 'All follow me'
        endif    
endfor

overhead "Enjoy your copy!" 104
stop